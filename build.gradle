plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.4.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'com.github.ben-manes.versions' version '0.20.0'
}

version = '0.1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    compile.exclude module: 'spring-boot-starter-logging'
    compile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter')
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-data-redis')
    compile('org.springframework.boot:spring-boot-starter-freemarker')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.session:spring-session-data-redis')
    compile('org.springframework.boot:spring-boot-starter-validation')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-log4j2')
    runtime('org.springframework.boot:spring-boot-devtools')
    testCompile('org.springframework.boot:spring-boot-starter-test')

    // Automatically generate configuration meta-data file from items annotated with @ConfigurationProperties
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')

    compile('org.postgresql:postgresql:42.2.4')

    compile "org.flywaydb:flyway-core:5.1.4"

    compile('joda-time:joda-time:2.10')

    compile 'org.asciidoctor:asciidoctorj:1.5.7'

    compileOnly 'org.projectlombok:lombok:1.18.2'
}

task assetClean(type: Delete, group: 'assets') {
    description 'Clean built assets'
    delete "${buildDir}/assets"
}

task assetCompile(type: Exec, group: 'assets') {
    description 'Compile assets'
    dependsOn assetClean
    mkdir buildDir
    commandLine 'broccoli', 'build', "${buildDir}/assets"
}

// Without this directive any additional-spring-configuration-metadata.json files will not be processed
compileJava.dependsOn(processResources)

bootJar {
    dependsOn assetCompile
    from 'build/assets', { into 'assets' }
    requiresUnpack '**/jruby-complete*.jar', '**/asciidoctorj-*.jar'
}
